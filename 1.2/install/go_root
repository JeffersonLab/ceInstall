#!/bin/csh -f


# Additional settings at the top of get_coptions
source get_coptions env

 # Options
set name     = root
set release  = `./get_coptions release`
set repo     = `./get_coptions repo`/root
set version  = $ROOT_VERSION
set filename = "root_v"$ROOT_VERSION".source.tar.gz"
set arch     = `./get_coptions arch`
set opt      = `./get_coptions copt`
./get_coptions log $name $version $filename $release $arch $opt

# Checking if the OS is 32 bit.
set oarch = ""
if($arch == "32" && `uname` == "Darwin") then
set oarch = '--disable-dependency-tracking'
endif

echo " > Special Compilation Options: " $oarch
echo

# System Versioning - necessary for Mac/Linux, 32/64 bit
set System = ""
	 if(`echo $release | grep mac   | grep i386 | wc | awk '{print $1}'` == "1") then
	set System = "macosx"
else if(`echo $release | grep mac   | grep 64   | wc | awk '{print $1}'` == "1") then
	set System = "macosx64"
else if(`echo $release | grep Linux | grep i386 | wc | awk '{print $1}'` == "1") then
	set System = "linux"
else if(`echo $release | grep Linux | grep i686 | wc | awk '{print $1}'` == "1") then
	set System = "linux"
else if(`echo $release | grep Linux | grep 64   | wc | awk '{print $1}'` == "1") then
	set System = "linuxx8664gcc"
endif

echo " > ROOT System:  " $System
echo


if($1 == "src"|| $1 == "make") goto $1

# Source Build
src:
	# Getting source file
	echo " > Installing root version "$version" - release "$release". Getting and unpacking" $filename"..."

	# Getting and unpacking file. For ROOT, we need source dir different than destination dir IMPORTANT
	rm -rf   $ROOTSYS
	mkdir -p $ROOTSYS
	# Unzipping file in $JLAB_SOFTWARE/root
	cd $ROOTSYS
	rm -f      $filename
	wget $repo/$filename
	tar -zxpvf $filename > /dev/null
if($1 == "src") exit

# notes: ROOT switched to cmake
make:
	# Configuring and Compiling
	# ROOT configure will give error on Mac if X11R6 is a link to X11
	# Even if XFT is disabled!
	set arg_root_conf = ""
	if($System == "macosx" || $System == "macosx64") then
		set arg_root_conf = "--with-xft-libdir=/usr/X11R6/lib"
	endif

	echo " > Configuring and compiling root source (log will be in "\$ROOTSYS/"make_log)..."
	cd $ROOTSYS/root
	./configure $System --disable-krb5 --disable-ldap --disable-xft  --disable-opengl --disable-fftw3 \
						--disable-afs  --disable-alien --disable-xrootd --disable-ssl  --disable-qtgsi --enable-gdml \
						--enable-soversion  --enable-python --enable-minuit2 --enable-roofit  \
						--prefix=$ROOTSYS --etcdir=$ROOTSYS $arg_root_conf > $ROOTSYS/make_log

	make         $opt >> $ROOTSYS/make_log
	make install $opt >> $ROOTSYS/make_log
if($1 == "make") exit


# Cleaning up
rm -rf $ROOTSYS/root

# Done!
echo done
echo











