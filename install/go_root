#!/bin/csh -f

# At JLab Robert Michaels is taking care of the ROOT installation
# chgrp -R 12gev_phys root
# chmod -R 775 root
#
# Everywhere else root is installed with brew or yum


# write permissions for JLab 12gev group
if($1 == "perm") then
	cd $JLAB_SOFTWARE
	mkdir root
	chgrp -R 12gev_phys root
	chmod -R 775 root
	cd root
	echo install root version $ROOT_VERSION in `pwd`
	exit
endif


set getCoptions = $JLAB_ROOT/$JLAB_VERSION/install/get_coptions
set release  = `$getCoptions release`
set system = ""
if(`echo $release | grep mac | wc | awk '{print $1}'` == "1") then
	set system = "Mac"
else if(`echo $release | grep Linux | wc | awk '{print $1}'` == "1") then
	set system = "Linux"
endif

if($1 == "install") then

	rm -rf   $ROOTSYS
	mkdir -p $ROOTSYS

	cd $ROOTSYS/..
	git clone --branch latest-stable https://github.com/root-project/root.git root_src
	mkdir root_build root_install
	cd root_build
	cmake -DCMAKE_INSTALL_PREFIX=../$ROOT_VERSION ../root_src # check cmake configuration output for warnings or error
	cmake --build . -- install -j8

endif

if($system == "Mac") then

	set rootVersion = `\ls -rt /usr/local/Cellar/root | tail -1`
	set SROOT = /usr/local/Cellar/root/$rootVersion
	echo
	echo System ROOT, version $rootVersion
	echo Linking to $SROOT installation.
	echo

	rm -rf   $ROOTSYS
	mkdir -p $ROOTSYS

#	 linking the relevant stuff
	ln -sfn $SROOT/lib     $ROOTSYS/lib
	ln -sfn $SROOT/include $ROOTSYS/include
	ln -sfn $SROOT/bin     $ROOTSYS/bin
	ln -sfn $SROOT/etc     $ROOTSYS/etc
	ln -sfn $SROOT/share   $ROOTSYS/share

# Using system QT for Linux.
else if($system == "Linux") then

	rm -rf   $ROOTSYS
	mkdir -p $ROOTSYS

	# explicit path to qmake
	set systemLibROOT     = /usr/lib64/root
	set systemIncludeROOT = /usr/include/root
	set systemBinROOT     = /usr/bin
	set systemShareROOT   = /usr/share/root

	echo
	echo System ROOT, linking to system in /usr
	echo

	ln -sfn $systemLibROOT     $ROOTSYS/lib
	ln -sfn $systemIncludeROOT $ROOTSYS/include
	ln -sfn $systemBinROOT     $ROOTSYS/bin
	ln -sfn $systemShareROOT   $ROOTSYS/share

endif

echo Done!
echo
