#!/bin/csh -f

# On some machine libiconv is needed. 
# The option WITHICONV (all capitals) will activate that library

# Additional settings at the top of get_coptions
source get_coptions env
set getCoptions = $JLAB_ROOT/$JLAB_VERSION/install/get_coptions

# Options
set name     = xercesc
set release  = `$getCoptions release`
set repo     = `$getCoptions repo`/xercesc
set version  = $XERCESC_VERSION
set filename = xerces-c-$XERCESC_VERSION".tar.gz"
set opt      = `$getCoptions copt`
set blog     = `$getCoptions logFile xercesc`
set blogS    = `$getCoptions logFileS xercesc`

# Write options on screen
$getCoptions log $name $version $filename $release $opt

if($1 == "src"|| $1 == "make") goto $1


# Source Build
src:
	# start message
	$getCoptions installLog $name $version

	# creating build and source dirs
	$getCoptions removeAndCreate $XERCESCROOT/build
	$getCoptions removeAndCreate $XERCESCROOT/source

	# unpacking inside source
	cd $XERCESCROOT/source
	$getCoptions getAndUnpack $repo $filename
if($1 == "src") exit

# make
make:

	set sourceDir = $XERCESCROOT"/source/xerces-c-"$version
	set buildDir  = $XERCESCROOT/build
	cd $buildDir

	# log start of compilation
	$getCoptions logStart $blog $blogS

	cmake -DCMAKE_INSTALL_PREFIX=$XERCESCROOT $sourceDir >> $blog

	make $opt    >> $blog
	make install >> $blog

	# log end of compilation, total time
	$getCoptions logEnd  $blog
	$getCoptions logTime $blog

if($1 == "make") exit

# Cleaning up
echo " > Cleaning up..."
cd $XERCESCROOT
rm -rf $version $filename

# Done!
echo " > Done."

# Notice: If you had undefined reference to libiconv_open warnings, try the option WITHICONV
#  ./go_xercesc WITHICONV


