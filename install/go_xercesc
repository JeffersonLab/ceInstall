#!/bin/csh -f

# On some machine libiconv is needed. 
# The option WITHICONV (all capitals) will activate that library

# Additional settings at the top of get_coptions
source get_coptions env

# Options
set name     = xercesc
set release  = `./get_coptions release`
set repo     = `./get_coptions repo`/xercesc
set version  = $XERCESC_VERSION
set filename = xerces-c-$XERCESC_VERSION".tar.gz"
set arch     = `./get_coptions arch`
set opt      = `./get_coptions copt`
set blog     = `./get_coptions logFile clhep`
set blogS    = `./get_coptions logFileS clhep`

# Write options on screen
./get_coptions log $name $version $filename $release $arch $opt

if($1 == "src"|| $1 == "make") goto $1


# Source Build
src:
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions installLog $name $version

	# creating dirs
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions removeAndCreate $XERCESCROOT
	cd $XERCESCROOT

	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions removeAndCreate build
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions removeAndCreate source

	# unpacking inside source
	cd source
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions getAndUnpack $repo $filename
if($1 == "src") exit

# make
make:

	set sourceDir = $XERCESCROOT"/xerces-"$version
	set buildDir  = $XERCESCROOT/build
	cd $buildDir

	# log start of compilation
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions logStart $blog $blogS

	if($1 == "WITHICONV" || $2 == "WITHICONV") then
    	    ./configure -prefix=$XERCESCROOT  DFLAGS="-L/usr/local/lib" LIBS="-liconv" >> $blog
	else
		./configure -prefix=$XERCESCROOT $sourceDir >> $blog
	endif

	make $opt    >> $blog
	make install >> $blog

	# log end of compilation, total time
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions logEnd  $blog
	$JLAB_ROOT/$JLAB_VERSION/install/get_coptions logTime $blog

if($1 == "make") exit

# Cleaning up
echo " > Cleaning up..."
cd $XERCESCROOT
rm -rf $version $filename

# Done!
echo " > Done."

# Notice: If you had undefined reference to libiconv_open warnings, try the option WITHICONV
#  ./go_xercesc WITHICONV


