#!/bin/csh -f

# library from http://proj-clhep.web.cern.ch/proj-clhep/

# Additional settings at the top of get_coptions
source get_coptions env
set getCoptions = $JLAB_ROOT/$JLAB_VERSION/install/get_coptions

# Options
set name     = clhep
set release  = `$getCoptions release`
set repo     = `$getCoptions repo`/clhep
set version  = $CLHEP_VERSION
set filename =  clhep"-"$CLHEP_VERSION".tgz"
set opt      = `$getCoptions copt`
set blog     = `$getCoptions logFile clhep`
set blogS    = `$getCoptions logFileS clhep`

# Write options on screen
$getCoptions log $name $version $filename $release $opt

if($1 == "src"|| $1 == "make") goto $1

# Source Build
src:
	# start message
	$getCoptions installLog $name $version

	# creating build and source dirs
	$getCoptions removeAndCreate $CLHEP_BASE_DIR/../source
	$getCoptions removeAndCreate $CLHEP_BASE_DIR/../build

	# unpacking inside source
	cd $CLHEP_BASE_DIR/../source
	$getCoptions getAndUnpack $repo $filename
if($1 == "src") exit

# make
make:

	set sourceDir = $CLHEP_BASE_DIR/../source/$version/CLHEP
	set buildDir  = $CLHEP_BASE_DIR/../build
	cd $buildDir

	# log start of compilation
	$getCoptions logStart $blog $blogS

	cmake -DCMAKE_INSTALL_PREFIX=$CLHEP_BASE_DIR  $sourceDir >> $blog

	make $opt    >> $blog
	make install >> $blog

	# log end of compilation, total time
	$getCoptions logEnd  $blog
	$getCoptions logTime $blog

if($1 == "make") exit

# Cleaning up
echo " > Cleaning up..."
cd $CLHEP_BASE_DIR
# uncomment this if they need debugging
rm -rf $version $filename ../build ../source

# Changing permission of bin (maybe they'll fix this with the next clhep release)
chmod a+x $CLHEP_BASE_DIR/bin/*

# Done
echo " > Done."
echo




















