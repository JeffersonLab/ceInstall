#!/bin/csh -f

# Additional settings at the top of get_coptions
source get_coptions env
set getCoptions = $JLAB_ROOT/$JLAB_VERSION/install/get_coptions 

# Options, Screen Log, Warnings
set release  = `$getCoptions release`
set repo     = `$getCoptions repo`/qt

set system = ""
set filename = ""
if(`echo $release | grep mac | wc | awk '{print $1}'` == "1") then
	set filename = "qt-opensource-mac-x64-"$QT_VERSION".dmg"
	set system = "Mac"
else if(`echo $release | grep Linux | wc | awk '{print $1}'` == "1") then
	set filename = "qt-opensource-linux-x64-"$QT_VERSION".run"
	set system = "Linux"
endif

# if QT_VERSION is system, link to the system qt
# As of Aug 28 2018 this is the default mode
if($QT_VERSION == "system") then

	if($system == "Mac") then

		set majorQtVersion = `\ls -rt /usr/local/Cellar | grep qt`
		set minorQtVersion = `\ls -rt /usr/local/Cellar/$majorQtVersion | tail -1`
		set SQTDIR = /usr/local/Cellar/$majorQtVersion/$minorQtVersion
		echo
		echo System QT, version $minorQtVersion selected
		echo Linking to $SQTDIR installation.
		echo

		rm -rf   $QTDIR
		mkdir -p $QTDIR

		# linking the relevant stuff
		ln -sfn $SQTDIR/lib     $QTDIR/lib
		ln -sfn $SQTDIR/include $QTDIR/include
		ln -sfn $SQTDIR/bin     $QTDIR/bin
		ln -sfn $SQTDIR/mkspecs $QTDIR/mkspecs
		ln -sfn $SQTDIR/plugins $QTDIR/plugins

	# Using system QT for Linux.
	else if($system == "Linux") then

		set minorQtVersion = `qmake --version | grep Qt | awk '{print $4}'`

		echo
		echo System QT, system version $minorQtVersion will be used
		echo Linking to $SQTDIR installation.
		echo

		# RedHat - Fedora - CentOS line
		if( -f /etc/fedora-release || -f /etc/redhat-release) then

			set LQTDIR = /usr/lib64        # libraries like libQt5Xml.so
			set IQTDIR = /usr/include/qt5  # includes
			set SQTDIR = $LQTDIR/qt5       # binaries, mkspecs and plugins

			# linking the relevant stuff
			ln -sfn $LQTDIR         $QTDIR/lib
			ln -sfn $IQTDIR         $QTDIR/include
			ln -sfn $SQTDIR/bin     $QTDIR/bin
			ln -sfn $SQTDIR/mkspecs $QTDIR/mkspecs
			ln -sfn $SQTDIR/plugins $QTDIR/plugins


		# Ubuntu line NEEDS TO BE VERIFIED
		else if( -f /etc/os-release) then

			set LQTDIR = /usr/x86_64-linux-gnu # libraries like libQt5Xml.so
			set IQTDIR = /usr/include/qt5      # includes
			set SQTDIR = $LQTDIR/qt5           # binaries, mkspecs and plugins

			# linking the relevant stuff
			ln -sfn $LQTDIR         $QTDIR/lib
			ln -sfn $IQTDIR         $QTDIR/include
			ln -sfn $SQTDIR/bin     $QTDIR/bin
			ln -sfn $SQTDIR/mkspecs $QTDIR/mkspecs
			ln -sfn $SQTDIR/plugins $QTDIR/plugins
		endif
	endif

	echo Done!
	echo
else
	# Using offline Installer
	echo
	echo Installing QT using the offline installer.
	echo
	echo $system detected. Downloading $filename
	echo

	rm $filename
	wget $repo/$filename
	chmod u+x $filename

	echo Download Successfull. Please execute:
	echo
	if($system == "Mac") then
		echo open $filename
	else if($system == "Linux") then
		echo ./$filename
	endif
	echo
	echo Then follow the installation instructions. You can skip the creation of a qt account.
	echo
	echo \!\!\! WARNING WARNING WARNING \!\!\!
	echo
	echo The qt installation program will ask to verify the installation folder.
	echo Make sure to clear the default installation location and copy/parse this path instead:
	echo
	echo $JLAB_SOFTWARE/qt/$QT_VERSION
	echo
	echo Make sure to include the Source Components during the installation process.
	echo
endif


