# JLAB12 Specific Software Environment

## BIN
set path = ($JLAB_SOFTWARE/bin $path) 


## GEMC
if( ! $?GEMC_VERSION || $overwrite == "yes" || ! $?GEMC ) then
	setenv GEMC_VERSION $DEFAULT_GEMC_VERSION
	setenv GEMC $JLAB_SOFTWARE/gemc/$GEMC_VERSION
else if ($GEMC_VERSION != $DEFAULT_GEMC_VERSION) then
	echo " > GEMC already set at "$GEMC_VERSION" instead of "$DEFAULT_GEMC_VERSION"."
endif

setenv GEMC_HOST      clasdb.jlab.org
setenv GEMC_USER      clasuser

if( ! -d $GEMC ) then
	echo " ? Attention: No GEMC installation found."
else
	echo " > GEMC    version: "$GEMC_VERSION
endif
set path = ($GEMC/source $GEMC/io $path)


## JANA
if( ! $?JANA_VERSION || $overwrite == "yes") then
	setenv JANA_VERSION $DEFAULT_JANA_VERSION
else if ($JANA_VERSION != $DEFAULT_JANA_VERSION) then
	echo " JANA already set at "$JANA_VERSION" instead of "$DEFAULT_JANA_VERSION"."
endif

setenv JANA_HOME $JLAB_SOFTWARE/jana/$JANA_VERSION

if( ! -d $JANA_HOME ) then
	echo " ? Attention: No JANA installation found."
else
	echo " > JANA    version: "$JANA_VERSION
endif


## Scons Scripts
if( ! $?SCONS_BM_VERSION || $overwrite == "yes") then
	setenv SCONS_BM_VERSION $DEFAULT_SCONS_BM_VERSION
else if ($SCONS_BM_VERSION != $DEFAULT_SCONS_BM_VERSION) then
	echo " the SCONS build system is already set at "$SCONS_BM_VERSION" instead of "$DEFAULT_SCONS_BM_VERSION"."
endif
if( ! -d $JLAB_ROOT/$JLAB_VERSION/scons_bm/$SCONS_BM_VERSION ) then
	echo " ? Attention: No Scons Script installation found."
else
	echo " > Build   version: "$SCONS_BM_VERSION
endif
setenv PYTHONPATH ${PYTHONPATH}:${JLAB_ROOT}/${JLAB_VERSION}/scons_bm/$SCONS_BM_VERSION
setenv SCONSFLAGS  "--site-dir=${JLAB_ROOT}/${JLAB_VERSION}/scons_bm/$SCONS_BM_VERSION"


## EVIO
if( ! $?EVIO_VERSION || $overwrite == "yes") then
	setenv EVIO_VERSION $DEFAULT_EVIO_VERSION
else if ($EVIO_VERSION != $DEFAULT_EVIO_VERSION) then
	echo " EVIO already set at "$EVIO_VERSION" instead of "$DEFAULT_EVIO_VERSION"."
endif

setenv EVIO $JLAB_SOFTWARE/evio/$EVIO_VERSION
set path = ($EVIO/bin $path)

if( ! -d $EVIO/lib ) then
	echo " ? Attention: No EVIO installation found."
else
	echo " > EVIO    version: "$EVIO_VERSION
endif
setenv LD_LIBRARY_PATH ${EVIO}/lib:${LD_LIBRARY_PATH}

## CCDB
# It needs python version 2.7
# The related path lines will be removed once 2.7 is default at JLab
if( ! $?CCDB_VERSION || $overwrite == "yes") then
	setenv CCDB_VERSION $DEFAULT_CCDB_VERSION
else if ($CCDB_VERSION != $DEFAULT_CCDB_VERSION) then
	echo " CCDB already set at "$CCDB_VERSION" instead of "$DEFAULT_CCDB_VERSION"."
endif

setenv CCDB_HOME $JLAB_SOFTWARE/ccdb/ccdb"-"$CCDB_VERSION".00"
set path = ($CCDB_HOME/bin $path)
setenv LD_LIBRARY_PATH ${CCDB_HOME}/lib:${LD_LIBRARY_PATH}
setenv PYTHONPATH ${PYTHONPATH}:${CCDB_HOME}/python

## python2.7
## temp fixes for JLab
setenv PATH /apps/python/PRO/bin:$PATH
setenv LD_LIBRARY_PATH /apps/python/PRO/lib:$LD_LIBRARY_PATH

## user
setenv CCDB_USER $USER

if( ! -d $CCDB_HOME/lib ) then
	echo " ? Attention: No CCDB installation found."
else
	echo " > CCDB    version: "$CCDB_VERSION
endif


## BANKS
# Looking for user defined BANKS_VERSION
if( ! $?BANKS_VERSION || $overwrite == "yes") then
	setenv BANKS_VERSION $DEFAULT_BANKS_VERSION
else if ($BANKS_VERSION != $DEFAULT_BANKS_VERSION) then
	echo " BANKS already set at "$BANKS_VERSION" instead of "$DEFAULT_BANKS_VERSION"."
endif

setenv BANKS    $JLAB_SOFTWARE/banks/$BANKS_VERSION
set path = ($BANKS/bin $path)

if( ! -d $BANKS/lib ) then
	echo " ? Attention: No BANKS installation found."
else
	set path = ($BANKS/bin $path)
	echo " > Banks   version: "$BANKS_VERSION
endif




