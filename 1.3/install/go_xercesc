#!/bin/csh -f

# On some machine libiconv is needed. 
# The option WITHICONV (all capitals) will activate that library

# Additional settings at the top of get_coptions
source get_coptions env

# Options
set name     = xercesc
set release  = `./get_coptions release`
set repo     = `./get_coptions repo`/xercesc
set version  = $XERCESC_VERSION
set filename = xerces-c-$XERCESC_VERSION".tar.gz"
set arch     = `./get_coptions arch`
set opt      = `./get_coptions copt`
./get_coptions log $name $version $filename $release $arch $opt

# Checking if the OS is 32 bit. 
set oarch = ""
if($arch == "32" && `uname` == "Darwin") then
	set oarch = '--disable-dependency-tracking'
endif

echo " > Special Compilation Options: " $oarch 
echo

if($1 == "src"|| $1 == "make") goto $1


# Source Build
src:
	# Getting source file
	echo " > Installing xerces version "$version" - release "$release". Getting and unpacking" $filename"..."
	rm -rf   $XERCESCROOT
	mkdir -p $XERCESCROOT

	# Unpacking file. For XERCES, we need source dir different than destination dir
	cd $XERCESCROOT
	rm -f       $filename
	wget  $repo/$filename
	tar -zxpvf  $filename >& /dev/null
if($1 == "src") exit

# make
make:
	set xercesdir = `\ls $XERCESCROOT | grep -v gz | grep -v log | grep -v include | grep -v lib | grep -v bin`
	# Configuring and Compiling
	echo " > Configuring and compiling XERCES source (log will be in "\$XERCESCROOT/"make_log)..."

	cd $XERCESCROOT/$xercesdir/

	if($arch == "32" && `uname` == "Darwin") then
		./configure $oarch CXXFLAGS="-arch i386" CFLAGS="-arch i386"  -prefix=$XERCESCROOT  > $XERCESCROOT/make_log
	else if($1 == "WITHICONV" || $2 == "WITHICONV") then
    	    ./configure $oarch -prefix=$XERCESCROOT  DFLAGS="-L/usr/local/lib" LIBS="-liconv" > $XERCESCROOT/make_log
	else
		./configure $oarch -prefix=$XERCESCROOT  > $XERCESCROOT/make_log
	endif

	make $opt    >> $XERCESCROOT/make_log
	make install >> $XERCESCROOT/make_log
if($1 == "make") exit

# Cleaning up
cd $XERCESCROOT
rm -rf $version $filename

# Done!
echo done
echo

