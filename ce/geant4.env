## GEANT4 environment

# set the version according to user preferences
setenv GEANT4_VERSION `getVersion.py GEANT4_VERSION $DEFAULT_GEANT4_VERSION $overwrite`

# envs are needed to run geant4
setenv G4ROOT         $JLAB_SOFTWARE/geant4
setenv G4INSTALL      $G4ROOT/$GEANT4_VERSION

set PROPOSEDINSTALL = $G4INSTALL

# set the location according to user preferences
setenv G4INSTALL `getVersion.py G4INSTALL $PROPOSEDINSTALL $overwrite`

# verify installation and print log
checkSoftware.py GEANT4 $G4INSTALL $PROPOSEDINSTALL $GEANT4_VERSION $DEFAULT_GEANT4_VERSION $G4INSTALL/bin

# complete setting the environment
# setenv G4VERSION      $GEANT4_VERSION # needed for examples? Verify

set path = ($G4INSTALL/bin $path)
if( -d $G4INSTALL/bin ) then
	setenv G4LIB `$G4INSTALL/bin/geant4-config --libs | awk '{print $1}' | awk -F"-L" '{print $2}'`
	setenv LD_LIBRARY_PATH ${G4LIB}:${LD_LIBRARY_PATH}
endif

## MARK: Geant4 data
# not elegant but we need data under G4INSTALL. Geant4 wants this structure
setenv G4DATA_VERSION $DEFAULT_G4DATA_VERSION
setenv G4DATA_DIR $G4INSTALL/data/Geant4-$G4DATA_VERSION/data

# G4DATA_DIR must be present to set the various env.
# Notice: G4ENSDFSTATEDATA is NOT set
if ( -d ${G4DATA_DIR} ) then
	setenv G4NEUTRONHPDATA    $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4NDL`             # Data for neutron scattering processes
	setenv G4LEDATA           $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4EMLOW`           # Data for low energy electromagnetic processes
	setenv G4LEVELGAMMADATA   $G4DATA_DIR/`\ls $G4DATA_DIR| grep PhotonEvaporation` # Nuclear Photon evaporation data
	setenv G4RADIOACTIVEDATA  $G4DATA_DIR/`\ls $G4DATA_DIR| grep RadioactiveDecay`  # Data for radioactive decay hadronic processes
	setenv G4SAIDXSDATA       $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4SAIDDATA`        # Data for evaluated cross-sections in SAID data-base
	setenv G4PARTICLEXSDATA   $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4PARTICLEXS`      # Data for evaluated neutron cross sections on natural composition of elements
	setenv G4ABLADATA         $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4ABLA`            # Data data files for nuclear shell effects in INCL/ABLA hadronic mode -
	setenv G4INCLDATA         $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4INCL`            # Data data files for proton and neutron density profiles in INCL -
	setenv G4PIIDATA          $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4PII`             # Data for shell ionisation cross-sections
	setenv G4ENSDFSTATEDATA   $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4ENSDFSTATE`      # Data for for nuclides properties
	setenv G4REALSURFACEDATA  $G4DATA_DIR/`\ls $G4DATA_DIR| grep RealSurface`       # Data measured optical surface reflectance in optical processes

	# need to be inside the loop coz the env need to be defined
	if( ! -d ${G4NEUTRONHPDATA}   ) echo " ? Attention: No G4NEUTRONHPDATA."
	if( ! -d ${G4LEDATA}          ) echo " ? Attention: No G4LEDATA."
	if( ! -d ${G4LEVELGAMMADATA}  ) echo " ? Attention: No G4LEVELGAMMADATA."
	if( ! -d ${G4RADIOACTIVEDATA} ) echo " ? Attention: No G4RADIOACTIVEDATA."
	if( ! -d ${G4SAIDXSDATA}      ) echo " ? Attention: No G4SAIDXSDATA."
	if( ! -d ${G4PARTICLEXSDATA}  ) echo " ? Attention: No G4PARTICLEXSDATA."
	if( ! -d ${G4ABLADATA}        ) echo " ? Attention: No G4ABLADATA."
	if( ! -d ${G4INCLDATA}        ) echo " ? Attention: No G4INCLDATA."
	if( ! -d ${G4PIIDATA}         ) echo " ? Attention: No G4PIIDATA."
	if( ! -d ${G4ENSDFSTATEDATA}  ) echo " ? Attention: No G4ENSDFSTATE."
	if( ! -d ${G4REALSURFACEDATA} ) echo " ? Attention: No G4REALSURFACEDATA."
# setting things by hand for the installer.
# Notice: run printVersion.csh after geant4 installation and paste the result here
# Or just check the version on the geant4 website
else
	setenv G4NEUTRONHPDATA   $G4DATA_DIR/G4NDL4.6
	setenv G4LEDATA          $G4DATA_DIR/G4EMLOW7.9
	setenv G4LEVELGAMMADATA  $G4DATA_DIR/PhotonEvaporation5.5
	setenv G4RADIOACTIVEDATA $G4DATA_DIR/RadioactiveDecay5.4
	setenv G4SAIDXSDATA      $G4DATA_DIR/G4SAIDDATA2.0
	setenv G4PARTICLEXSDATA  $G4DATA_DIR/G4PARTICLEXS2.1
	setenv G4ABLADATA        $G4DATA_DIR/G4ABLA3.1
	setenv G4INCLDATA        $G4DATA_DIR/G4INCL1.0
	setenv G4PIIDATA         $G4DATA_DIR/G4PII1.3
	setenv G4ENSDFSTATEDATA  $G4DATA_DIR/G4ENSDFSTATE2.2
	setenv G4REALSURFACEDATA $G4DATA_DIR/RealSurface2.1.1
endif




