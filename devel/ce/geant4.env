# Looking for user defined GEANT4_VERSION
if( ! $?GEANT4_VERSION || $overwrite == "yes") then
	setenv GEANT4_VERSION $DEFAULT_GEANT4_VERSION
else if ($GEANT4_VERSION != $DEFAULT_GEANT4_VERSION) then
	echo " ! GEANT4 already set at "$GEANT4_VERSION" instead of "$DEFAULT_GEANT4_VERSION"."
endif

# envs are needed to run geant4
setenv G4VERSION  $GEANT4_VERSION
setenv G4ROOT         $JLAB_SOFTWARE/geant4
setenv G4INSTALL      $G4ROOT/$G4VERSION
setenv G4DATA_VERSION $DEFAULT_G4DATA_VERSION

# not elegant but we need data under G4INSTALL. Geant4 wants this structure
setenv G4DATA_DIR $G4INSTALL/data/Geant4-$G4DATA_VERSION/data


set path = ($G4INSTALL/bin $path)

if( -d $G4INSTALL/bin ) then
	setenv G4LIB `$G4INSTALL/bin/geant4-config --libs | awk '{print $1}' | awk -F"-L" '{print $2}'`
	setenv LD_LIBRARY_PATH ${G4LIB}:${LD_LIBRARY_PATH}
endif

# System is used by the installation
if(`uname` == "Linux") then
	setenv G4SYSTEM Linux-g++
endif
if(`uname` == "Darwin") then
	setenv G4SYSTEM Darwin-g++
endif

if( ! -d $G4INSTALL/bin ) then
	echo " ? Attention: No GEANT4 installation found."
else

	echo " > Geant4  version:" $G4VERSION


	setenv G4LEDATA           $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4EMLOW`           # Data for low energy electromagnetic processes
	setenv G4LEVELGAMMADATA   $G4DATA_DIR/`\ls $G4DATA_DIR| grep PhotonEvaporation` # Nuclear Photon evaporation data
	setenv G4NEUTRONHPDATA    $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4NDL`             # Data for neutron scattering processes
	setenv G4NEUTRONXSDATA    $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4NEUTRONXS`       # Data for evaluated neutron cross sections on natural composition of elements
	setenv G4PIIDATA          $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4PII`             # Data for shell ionisation cross-sections
	setenv G4RADIOACTIVEDATA  $G4DATA_DIR/`\ls $G4DATA_DIR| grep RadioactiveDecay`  # Data for radioactive decay hadronic processes
	setenv G4REALSURFACEDATA  $G4DATA_DIR/`\ls $G4DATA_DIR| grep RealSurface`       # Data measured optical surface reflectance in optical processes
	setenv G4SAIDXSDATA       $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4SAIDDATA`        # Data for Said Database
	setenv G4ABLADATA         $G4DATA_DIR/`\ls $G4DATA_DIR| grep G4ABLA`            # Data for G4ABLA3.0

	if( ! -d ${G4LEDATA}          ) echo " ? Attention: No G4LEDATA."
	if( ! -d ${G4LEVELGAMMADATA}  ) echo " ? Attention: No G4LEVELGAMMADATA."
	if( ! -d ${G4NEUTRONHPDATA}   ) echo " ? Attention: No G4NEUTRONHPDATA."
	if( ! -d ${G4NEUTRONXSDATA}   ) echo " ? Attention: No G4NEUTRONXSDATA."
	if( ! -d ${G4PIIDATA}         ) echo " ? Attention: No G4PIIDATA."
	if( ! -d ${G4RADIOACTIVEDATA} ) echo " ? Attention: No G4RADIOACTIVEDATA."
	if( ! -d ${G4REALSURFACEDATA} ) echo " ? Attention: No G4REALSURFACEDATA."
	if( ! -d ${G4SAIDXSDATA}      ) echo " ? Attention: No G4SAIDXSDATA."
	if( ! -d ${G4ABLADATA}        ) echo " ? Attention: No G4ABLADATA."

endif




