#!/bin/csh -f

# library from http://proj-clhep.web.cern.ch/proj-clhep/

# Additional settings at the top of get_coptions
source get_coptions env

# Options
set name     = clhep
set release  = `./get_coptions release`
set repo     = `./get_coptions repo`/clhep
set version  = $CLHEP_VERSION
set filename =  clhep"-"$CLHEP_VERSION".tgz"
set arch     = `./get_coptions arch`
set opt      = `./get_coptions copt`
set cmakev   = `./get_coptions cmakev | awk '{print $1}'`

# Exit if cmake is not >= 3.3
if($cmakev != "OK") then
	./get_coptions cmakev
	exit
endif


# Write options on screen
./get_coptions log $name $version $filename $release $arch $opt


# Checking if the OS is 32 bit. 
set oarch = ""
if($arch == "32") then
	set oarch = '-DCMAKE_C_FLAGS=-m32 -DCMAKE_CXX_FLAGS=-m32 -DCPACK_SYSTEM_NAME=$OSRELEASE'
endif
echo " > Special Compilation Options: " $oarch 
echo


if($1 == "src"|| $1 == "make" || $1 == "fixconfig") goto $1

# Source Build
src:
	# Getting source file
	echo " > Installing clhep version "$version" - release "$release". Getting and unpacking" $filename"..."
	rm -rf   $CLHEP_BASE_DIR
	mkdir -p $CLHEP_BASE_DIR

	# Unpacking file. For CLHEP, we need source dir different than destination dir
	cd $CLHEP_BASE_DIR/..
	rm -rf build  ; mkdir build
	rm -rf source ; mkdir source
	cd source
	rm -f          $filename
	wget     $repo/$filename
	echo Unpacking $filename
	tar -zxpvf     $filename >& /dev/null
if($1 == "src") exit

# make
make:
	# Configuring and Compiling
	echo " > Configuring and compiling clhep source (log will be in "\$CLHEP_BASE_DIR/"make_log)..."
	cd $CLHEP_BASE_DIR/../build
	cmake $oarch -DCMAKE_INSTALL_PREFIX=$CLHEP_BASE_DIR  $CLHEP_BASE_DIR/../source/$version/CLHEP > $CLHEP_BASE_DIR/make_log

	make $opt    >> $CLHEP_BASE_DIR/make_log
	make install >> $CLHEP_BASE_DIR/make_log
if($1 == "make") exit

# Cleaning up
cd $CLHEP_BASE_DIR
rm -rf $version $filename ../build ../source

# Changing permission of bin (maybe they'll fix this with the next clhep release)
chmod a+x $CLHEP_BASE_DIR/bin/*

# Done!
echo done
echo




# Warning: on mac I get errors on the clhep script
# To fix:
# use the full name of this script to get the current path
# configpath=`dirname ${0}`
# binpath=${configpath}
fixconfig:
if(`uname` == "Darwin") then
	grep -B100 "# use the full name of this script to get the current path" $CLHEP_BASE_DIR/bin/clhep-config  > tmp
	echo 'configpath=`dirname ${0}`'                                                                         >> tmp
	echo 'binpath=${configpath}'                                                                             >> tmp
	grep -A200 "# OK - now we have to strip the trailing " $CLHEP_BASE_DIR/bin/clhep-config                  >> tmp
	mv tmp $CLHEP_BASE_DIR/bin/clhep-config
	chmod a+x  $CLHEP_BASE_DIR/bin/clhep-config
endif
if($1 == "fixconfig") exit




































